pkgname=('system-openbor')
pkgver=3.0.6391
pkgrel=1
pkgdesc="OpenBOR system configuration"
arch=('armv7h')
url="http://www.chronocrash.com"
license=('BSD')
makedepends=('git' 'yasm' 'imagemagick' 'glu' 'libvpx' 'libpng' 'libvorbis' 'sdl2_gfx')
optdepends=('libgl: OpenGL video mode')
source=(
  'openbor.cfg'
  'https://github.com/DCurrent/openbor/archive/v6391.tar.gz'
  '001.launch.as.param.patch'
  '002.latest.build.patch'
)
md5sums=('42580ba0cbc996424ab7504003eb51f0'
         'dd54afd42fc3134a34fa5ea6320614f5'
         'ded1a00ca76280dfc9deabed6661e4b5'
	 '1efd1ac1026f51858b1fe7573f9b39b4')

prepare()
{
  cd ${srcdir}/openbor-6391/

#  patch -p0 <../001.launch.as.param.patch

  cd engine
  patch -p0 <../../002.latest.build.patch


  # disable abort on warnings/errors
  sed 's/-Werror//' -i Makefile
  # disable RPATH
  sed 's/-Wl,-rpath,$(LIBRARIES)//' -i Makefile
  # verbose output
  #sed 's/@$(CC)/$(CC)/' -i Makefile
  # fix a locale warning
  sed 's|en_US.UTF-8|C|g' -i version.sh
}

build()
{
  cd ${srcdir}/openbor-6391/engine

  # work around 'special' build system
  ./version.sh
  make SDKPATH=/usr LNXDEV=/usr/bin BUILD_PANDORA=1 GCC_TARGET=$CARCH
}

package()
{
  mkdir -p $pkgdir/system/configs/emulationstation/systems.d "$pkgdir"/roms/openbor/.data/lib/
  install -Dm600 openbor.cfg $pkgdir/system/configs/emulationstation/systems.d/openbor.cfg

  # launcher + binary
  install -Dm755 "${srcdir}"/openbor-6391/engine/OpenBOR "$pkgdir"/roms/openbor/.data/OpenBOR
  install -Dm755 /usr/lib/{libSDL2_gfx-1.0.so.0,libm.so.6,libvpx.so.6,libvorbisidec.so.1} "$pkgdir"/roms/openbor/.data/lib/
}
